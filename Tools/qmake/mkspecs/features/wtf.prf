# -------------------------------------------------------------------
# This file contains shared rules used both when building against WTF
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

load(features)

WTF_SOURCE_DIR = $${ROOT_WEBKIT_DIR}/Source/JavaScriptCore/wtf

# Use a config-specific target to prevent parallel builds file clashes on Mac
mac: CONFIG(debug, debug|release): WTF_TARGET = wtfd
else: WTF_TARGET = wtf

# Output in WTF/<config>
CONFIG(debug, debug|release) : WTF_DESTDIR = debug
else: WTF_DESTDIR = release

WTF_INCLUDEPATH = \
    $$WTF_SOURCE_DIR/.. \
    $$WTF_SOURCE_DIR \
    $$WTF_SOURCE_DIR/gobject \
    $$WTF_SOURCE_DIR/qt \
    $$WTF_SOURCE_DIR/unicode

INCLUDEPATH = $$WTF_INCLUDEPATH $$INCLUDEPATH

VPATH += $$WTF_SOURCE_DIR

contains(CONFIG, use_system_icu) {
    DEFINES += WTF_USE_ICU_UNICODE=1
    DEFINES -= WTF_USE_QT4_UNICODE
    LIBS += -licuuc -licui18n
} else {
    DEFINES += WTF_USE_QT4_UNICODE=1
    DEFINES -= WTF_USE_ICU_UNICODE
}

linux-*:!contains(DEFINES, USE_QTMULTIMEDIA=1) {
    !contains(QT_CONFIG, no-pkg-config):system(pkg-config --exists glib-2.0 gio-2.0 gstreamer-0.10): {
        DEFINES += ENABLE_GLIB_SUPPORT=1
        PKGCONFIG += glib-2.0 gio-2.0
    }
}

needToLinkLibrary(WTF) {
    pathToWTFOutput = $${ROOT_BUILD_DIR}/Source/JavaScriptCore/wtf/$$WTF_DESTDIR
    linkLibrary($$pathToWTFOutput, $$WTF_TARGET)
}

