# -------------------------------------------------------------------
# This file is used by tools that rely on the QtWebKit library
#
# FIXME: Can we use the same way as client applications would use?
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

config_step {
    QMAKE_LIBDIR = $$ROOT_BUILD_DIR/lib $$QMAKE_LIBDIR
    QTWEBKITLIBNAME = QtWebKit
    mac:!static:contains(QT_CONFIG, qt_framework):!CONFIG(webkit_no_framework) {
        LIBS += -framework $$QTWEBKITLIBNAME
        QMAKE_FRAMEWORKPATH = $$ROOT_BUILD_DIR/lib $$QMAKE_FRAMEWORKPATH
    } else {
        build_pass: win32-*|wince* {
            !CONFIG(release, debug|release): QTWEBKITLIBNAME = $${QTWEBKITLIBNAME}d
            QTWEBKITLIBNAME = $${QTWEBKITLIBNAME}$${QT_MAJOR_VERSION}
            win32-g++*: LIBS += -l$$QTWEBKITLIBNAME
            else: LIBS += $${QTWEBKITLIBNAME}.lib
        } else {
            LIBS += -lQtWebKit
        }
    }

    DEPENDPATH += $$ROOT_WEBKIT_DIR/Source/WebKit/qt/Api
    INCLUDEPATH += $${ROOT_BUILD_DIR}/include/QtWebKit

    force_static_libs_as_shared {
        !no_webkit2: LIBS += -lwebkit2
        LIBS += -lwebcore
        !v8: LIBS += -ljscore
        LIBS += -lwtf
    }
}

