# -------------------------------------------------------------------
# This file contains shared rules used both when building
# JavaScriptCore itself, and by targets that use JavaScriptCore.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

JAVASCRIPTCORE_SOURCE_DIR = $${ROOT_WEBKIT_DIR}/Source/JavaScriptCore

# Use a config-specific target to prevent parallel builds file clashes on Mac
mac: CONFIG(debug, debug|release): JAVASCRIPTCORE_TARGET = jscored
else: JAVASCRIPTCORE_TARGET = jscore

# Output in JavaScriptCore/<config>
CONFIG(debug, debug|release) : JAVASCRIPTCORE_DESTDIR = debug
else: JAVASCRIPTCORE_DESTDIR = release

JAVASCRIPTCORE_GENERATED_SOURCES_DIR = $${ROOT_BUILD_DIR}/Source/JavaScriptCore/$${GENERATED_SOURCES_DESTDIR}

JAVASCRIPTCORE_INCLUDEPATH = \
    $$JAVASCRIPTCORE_SOURCE_DIR \
    $$JAVASCRIPTCORE_SOURCE_DIR/.. \
    $$JAVASCRIPTCORE_SOURCE_DIR/assembler \
    $$JAVASCRIPTCORE_SOURCE_DIR/bytecode \
    $$JAVASCRIPTCORE_SOURCE_DIR/bytecompiler \
    $$JAVASCRIPTCORE_SOURCE_DIR/heap \
    $$JAVASCRIPTCORE_SOURCE_DIR/dfg \
    $$JAVASCRIPTCORE_SOURCE_DIR/debugger \
    $$JAVASCRIPTCORE_SOURCE_DIR/interpreter \
    $$JAVASCRIPTCORE_SOURCE_DIR/jit \
    $$JAVASCRIPTCORE_SOURCE_DIR/parser \
    $$JAVASCRIPTCORE_SOURCE_DIR/profiler \
    $$JAVASCRIPTCORE_SOURCE_DIR/runtime \
    $$JAVASCRIPTCORE_SOURCE_DIR/yarr \
    $$JAVASCRIPTCORE_SOURCE_DIR/API \
    $$JAVASCRIPTCORE_SOURCE_DIR/ForwardingHeaders \
    $$JAVASCRIPTCORE_GENERATED_SOURCES_DIR

VPATH += $$JAVASCRIPTCORE_SOURCE_DIR

INCLUDEPATH = $$JAVASCRIPTCORE_INCLUDEPATH $$INCLUDEPATH

win32-g++* {
    LIBS += -lpthreadGC2
} else:win32-msvc* {
    LIBS += -lpthreadVC2
}

win32-*: DEFINES += _HAS_TR1=0

wince* {
    INCLUDEPATH += $$QT_SOURCE_TREE/src/3rdparty/ce-compat
    INCLUDEPATH += $$JAVASCRIPTCORE_SOURCE_DIR/os-win32
}

needToLinkLibrary(JavaScriptCore) {
    pathToJavaScriptCoreOutput = $${ROOT_BUILD_DIR}/Source/JavaScriptCore/$$JAVASCRIPTCORE_DESTDIR
    linkLibrary($$pathToJavaScriptCoreOutput, $$JAVASCRIPTCORE_TARGET)

    win32-* {
        LIBS += -lwinmm
    }
}

