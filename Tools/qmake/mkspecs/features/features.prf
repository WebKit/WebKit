# -------------------------------------------------------------------
# This file does basic 'configure' style checks to determine the
# set of ENABLE_FOO defines and their values.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

# Since features.prf is currently loaded from .pro/.pri files, not
# defaults_pre.prf, we have at this point parsed any optional command
# line arguments to qmake (eg qmake DEFINES+=ENABLE_VIDEO=0), that are
# intended to override default values (static or dynamic) of the
# feature defines.
#
# This is why the checks below look for !contains(DEFINES, ENABLE_FOO=.)
# so that the static or dynamic defaults will only be set if the user did
# not make an explicit choice by passing an argument to qmake/build-webkit.

# ------------- Prepare for feature detection -------------

## load mobilityconfig if mobility is available
load(mobilityconfig, true)

# Try to locate sqlite3 source
SQLITE3SRCDIR = $$(SQLITE3SRCDIR)
isEmpty(SQLITE3SRCDIR) {
    SQLITE3SRCDIR = $$[QT_INSTALL_PREFIX]/src/3rdparty/sqlite/
}

# ---------- Dynamically detect optional features -------------
#
# Please note, static feature defaults go in features.pri
#
# Do not add new CONFIG options, but use the existing patterns
# of WTF_USE, ENABLE_, etc, and detect the precense of those
# defines in the relevant target files. You do not need to have
# a fallback in your logic for the disabling feature, that's handled
# by having the feature disabled in features.pri. Nor do you have to
# unset the opposite value, that's handled automatically for you.

# For Mac we do not rely on QT_CONFIG, because icu is not officially supported for all of qt.
haveQt(5):if(contains(QT_CONFIG,icu)|mac) {
    DEFINES += WTF_USE_ICU_UNICODE=1
    DEFINES -= WTF_USE_QT4_UNICODE=1
} else {
    DEFINES += WTF_USE_QT4_UNICODE=1
    DEFINES -= WTF_USE_ICU_UNICODE=1
}

!contains(DEFINES, HAVE_QRAWFONT=.) {
    # Use of QRawFont in Qt 5:
    haveQt(5): DEFINES += HAVE_QRAWFONT=1
}

# We need fontconfig to set up the test fonts for DumpRenderTree and WebKitTestRunner.
haveQt(5) {
    contains(config_test_fontconfig, yes):!mac {
        DEFINES += HAVE_FONTCONFIG=1
    }
} else {
    # Qt 4
    unix:!mac:!embedded {
        DEFINES += HAVE_FONTCONFIG=1
    }
}

# We need libxml2 config test to determine to use libxml2 or not
!contains(DEFINES, WTF_USE_LIBXML2=.) {
    haveQt(5):contains(config_test_libxml2, yes): DEFINES += WTF_USE_LIBXML2=1
}

# We need libxslt config test to determine to use libxslt or not
!contains(DEFINES, ENABLE_XSLT=.) {
    haveQt(5):contains(config_test_libxslt, yes): DEFINES += ENABLE_XSLT=1
}

!contains(DEFINES, WTF_USE_QT_IMAGE_DECODER=.): DEFINES += WTF_USE_QT_IMAGE_DECODER=1

# Tiled Backing Store support
!contains(DEFINES, WTF_USE_TILED_BACKING_STORE=.): DEFINES += WTF_USE_TILED_BACKING_STORE=1

# Nescape plugins support (NPAPI)
!contains(DEFINES, ENABLE_NETSCAPE_PLUGIN_API=.) {
    unix:haveQt(4)|win32-*:!embedded:!wince*: {
        DEFINES += ENABLE_NETSCAPE_PLUGIN_API=1
    }
}

# Netscape plugins support for WebKit2
!contains(DEFINES, PLUGIN_ARCHITECTURE_UNSUPPORTED=.) {
    no_webkit2 {
        DEFINES += PLUGIN_ARCHITECTURE_UNSUPPORTED=1
    } else {
        contains(QT_CONFIG, xcb-xlib) {
            CONFIG += plugin_backend_xlib
            DEFINES += PLUGIN_ARCHITECTURE_X11=1 \
                       PLUGIN_ARCHITECTURE_UNSUPPORTED=0 \
                       ENABLE_PLUGIN_PROCESS=1
        } else {
            DEFINES += PLUGIN_ARCHITECTURE_UNSUPPORTED=1
        }
    }
}

# QStyle detection
haveQt(4)|!isEmpty(QT.widgets.name): DEFINES += HAVE_QSTYLE=1

# WebGL support
!contains(DEFINES, ENABLE_WEBGL=.) {
    contains(QT_CONFIG, opengl):!win32-*: DEFINES += ENABLE_WEBGL=1
}

!contains(DEFINES, ENABLE_XSLT=.) {
    # Using QtXmlPatterns
    contains(QT_CONFIG, xmlpatterns): DEFINES += ENABLE_XSLT=1

    # Or libxml2 -- experimental and only works on Linux so far
    contains(DEFINES, WTF_USE_LIBXML2=1): DEFINES += ENABLE_XSLT=1
}

# Geolocation support if QtMobility exists
!contains(DEFINES, ENABLE_GEOLOCATION=.) {
    contains(MOBILITY_CONFIG, location)|contains(QT_CONFIG, location) {
       DEFINES += ENABLE_GEOLOCATION=1
    }
}

# orientation support
haveQt(5) {
    !isEmpty(QT.sensors.name) {
        !contains(DEFINES, ENABLE_ORIENTATION_EVENTS=.): DEFINES += ENABLE_ORIENTATION_EVENTS=1
        !contains(DEFINES, ENABLE_DEVICE_ORIENTATION=.): DEFINES += ENABLE_DEVICE_ORIENTATION=1
    }
} else {
    # Qt 4 with Qt Mobility
    contains(MOBILITY_CONFIG, sensors) {
        !contains(DEFINES, ENABLE_ORIENTATION_EVENTS=.): DEFINES += ENABLE_ORIENTATION_EVENTS=1
        !contains(DEFINES, ENABLE_DEVICE_ORIENTATION=.): DEFINES += ENABLE_DEVICE_ORIENTATION=1

        # FIXME: These should not be here, but in the target that needs them
        CONFIG *= mobility
        MOBILITY *= sensors
    }
}

# HTML5 Media Support
!contains(DEFINES, ENABLE_VIDEO=.) {
    mac:!contains(DEFINES, USE_QTMULTIMEDIA=1) {
        DEFINES += ENABLE_VIDEO=1
        DEFINES += WTF_USE_QTKIT=1
    } else: linux-*:!contains(DEFINES, USE_QTMULTIMEDIA=1) {
        !contains(QT_CONFIG, no-pkg-config):system(pkg-config --exists glib-2.0 gio-2.0 \'gstreamer-0.10 >= 0.10.30\' \'gstreamer-plugins-base-0.10 >= 0.10.30\'): {
            DEFINES += ENABLE_VIDEO=1
            DEFINES += WTF_USE_GSTREAMER=1
        } else {
            root_project_file: message("Missing GLib/Gio/GStreamer, disabling media element support")
        }
    } else: contains(MOBILITY_CONFIG, multimedia) {
        DEFINES += ENABLE_VIDEO=1
        DEFINES += WTF_USE_QT_MULTIMEDIA=1
    }
}

!contains(DEFINES, ENABLE_FULLSCREEN_API=.) {
    !no_webkit2: DEFINES += ENABLE_FULLSCREEN_API=1
}

haveQt(4) {
    haveQtModule(declarative): DEFINES += HAVE_QQUICK1=1
} else {
    # FIXME: Add Qt 5 QQuick1 support once QDeclarativeEngine in QQuick2
    # is called QmlEngine and it is safe for us to use QQuick1 again.
}

!contains(DEFINES, WTF_USE_GRAPHICS_SURFACE=.) {
    haveQt(5):mac: DEFINES += WTF_USE_GRAPHICS_SURFACE=1
}

# -------------- Fill in static defaults --------------
#
# Only applies to features that don't have a dynamic default
# or have been overriden on the command line.

include(features.pri)

for(feature, FEATURE_DEFAULTS) {
    anyFeatureDefine = $$replace(feature, =.$, =.)
    !contains(DEFINES, $$anyFeatureDefine) {
        DEFINES += $$feature
    }
}

# ---------------- Sanitize defines -------------------

# Disable SVG fonts if SVG is disabled or we don't have raw fonts
contains(DEFINES, ENABLE_SVG=0)|!contains(DEFINES, HAVE_QRAWFONT=1): DEFINES += ENABLE_SVG_FONTS=0

# JavaScript debugger requires the inspector
contains(DEFINES, ENABLE_INSPECTOR=0): DEFINES += ENABLE_JAVASCRIPT_DEBUGGER=0

# Remove conflicting defines. The preprocessor would take care of this
# anyways, but doing it here makes for nicer command lines when building.
for(define, DEFINES) {
    parts = $$split(define, =)
    value = $$member(parts, 1, 1)
    isEmpty(value): next()

    equals(value, 1): opposite = $$replace(define, =1$, =0)
    equals(value, 0): opposite = $$replace(define, =0$, =1)

    # Is there a later define that overrides this one?
    contains(DEFINES, $$opposite): DEFINES -= $$define
}

# --------- Make features available to JSC ------------

## Forward enabled feature macros to JavaScript enabled features macros
FEATURE_DEFINES_JAVASCRIPT = LANGUAGE_JAVASCRIPT=1
v8: FEATURE_DEFINES_JAVASCRIPT += V8_BINDING=1

for(define, DEFINES) {
    enabled_feature_define = $$find(define, ^ENABLE_.+=1$)
    isEmpty(enabled_feature_define): next()

    FEATURE_DEFINES_JAVASCRIPT += $$enabled_feature_define
}

# ---------------------- The end ----------------------

# Used to compute defaults for the build-webkit script
# Don't place anything after this!
CONFIG(compute_defaults) {
    message($$DEFINES)
    error("Done computing defaults")
}
